#!/usr/bin/perl
BEGIN { $ENV{TZ} = 'UTC' }

use local::lib;
use v5.10;
use strict;
use warnings;

use FindBin qw( $RealBin );
use lib "$RealBin/lib";

use File::Path qw( make_path );
use LogBot::Config qw( find_config load_all_configs load_config );
use LogBot::Database qw( dbh );
use LogBot::Util qw( logbot_init pretty_size timestamp );

my @configs;

if (@ARGV && $ARGV[0] eq '--all') {
    shift;
    @configs = values %{ load_all_configs() };
} else {
    push @configs, load_config(find_config(shift));
}

my $backup_path = shift;
(scalar(@configs) && $backup_path) || die "syntax: logbot-backup <config file|--all> <backup path>\n";
$backup_path =~ s#/$##;

foreach my $config (@configs) {
    logbot_init($config, quiet => 1);

    make_path($backup_path);
    my $backup_file = $backup_path . '/' . $config->{name} . '.sqlite';

    say timestamp(), ' backing up to ' . $backup_file if $ENV{DEBUG};
    my $dbh = dbh($config);
    $dbh->sqlite_backup_to_file($backup_file);

    say timestamp(), " compressing $backup_file (" . pretty_size(-s $backup_file) . ")" if $ENV{DEBUG};
    unlink($backup_file . '.bz2');
    system('bzip2', $backup_file);

    say timestamp(), " compressed (" . pretty_size(-s $backup_file . '.bz2') . ")" if $ENV{DEBUG};
}
